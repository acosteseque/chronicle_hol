rule high_risk_user_download_executable_from_macro {
  meta:
    author = "Google Cloud Security Demos"
    description = "Executable downlaoded by Microsoft Excel from High Risk User"
    severity = "High"
    technique = "T1204.002"

  events:
    //proxy event with suspected executable download
    $proxy_event.target.url =  /.*\.exe$/ or
    $proxy_event.network.received_bytes > 102400
    $proxy_event.principal.user.userid = $user
    $proxy_event.principal.user.userid != /test/

    //correlate with EDR event indicating Excel activity
    $edr_event.target.user.userid  = $user
    $edr_event.target.process.file.full_path = /excel/ nocase
    $edr_event.metadata.event_type = "NETWORK_CONNECTION"

    //First hop into entity graph to find the premissions
    $user_entity.graph.entity.user.userid = $user
    $user_entity.graph.relations.entity.resource.name = $table_name

    //second hop for the table context
    $table_context.graph.entity.resource.product_object_id = $table_name
    $table_context.graph.metadata.product_name = "Google Cloud DLP"

  match:
    $user over 5m

  outcome:
    //calculate risk score
    $risk_score = max(
      if ($table_context.graph.entity.resource.attribute.labels.value = "US_SOCIAL_SECURITY_NUMBER",80)
    )

  condition:
    $proxy_event and $edr_event and $user_entity and $table_context  
}
